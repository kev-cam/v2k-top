#!/bin/csh -f

set v2k  = v2k
set mode = ansi
set args = ()
set frst = ()
set last = ()
set libs = ()
set dbg  = ()

unset libp

while ($#argv)
  switch ($1)
    case -f:
      shift
      set f    = $1:q
      shift
      set argv = (- `egrep -v '^ *(//|#)' $f:q` $*:q)
      breaksw
    case -y:
      shift
      if $?libp then
        set libp = ${libp:q}:$1:q
      else
        set libp = $1:q
      endif
      set libs = (${libs:q} -V$1:q)
      breaksw
    case +define+*:
    case +incdir+*:
      set frst = (${frst:q} $1:q)
      breaksw
    case ++*:
      set last = (${last:q} $1:q)
      breaksw
    case -dbg:
      set dbg = ($1:q)
      breaksw
    case --xl:
      set mode = xl
      breaksw
    case --vcs:
      set mode = vcs
      breaksw
    case --ignore1:
      shift
      breaksw
    case -v:
      shift
      switch ($1:q)
        case *.v:
	  set args = ($args:q $1:q)
          breaksw
        default
	  set args = ($args:q -vrlg $1:q)
      endsw
      breaksw
    default:
      set args = ($args:q $1:q)
  endsw
  shift
end

if ($?libp) then
  setenv V2K_VLIB_PATH $libp:q
endif

exec $v2k $dbg:q $frst:q $libs:q $args:q $last:q
